/*
 * Copyright 2011 JBoss Inc
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *       http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

package org.drools.informer

import org.apache.commons.beanutils.PropertyUtils;

import org.drools.informer.domain.DomainModelSupport;

rule "derive answerType from the underlying domain model if it isn't specified"
when
	$question    : Question( questionId : id, answerType == null );
	$association : DomainModelAssociation( questionId == questionId, $o : object, $p : property );
then
	modify ( $question ) {
	    setAnswerType( DomainModelSupport.classToAnswerType( PropertyUtils.getPropertyType( $o, $p ) ) );
	}
end



rule "copy answers from the underlying domain model to the questions"
no-loop
salience 6
when
	$question    : Question( questionId : id, answerType != null, $ans : answer, $last : lastAnswer ) @watch( * )
	$association : DomainModelAssociation( questionId == questionId, lastAnswer == $last, object : object )
	$obj         : Object( this == object )
	$answer      : Object( this != $ans ) from DomainModelSupport.objectToAnswer( PropertyUtils.getProperty( $association.getObject(), $association.getProperty() ),
	                                                                              $question.getBasicAnswerType() );
then
    modify ( $association ) {
        setLastAnswer( $answer );
    }
    modify ( $question ) {
        setAnswer( $answer );
    }
end


rule "copy question answers to the underlying domain model"
salience 7
no-loop
when
	$question    : Question( questionId : id, answerType != null, $answer : answer != null ) @watch( lastAnswer )
	$association : DomainModelAssociation( questionId == questionId, $object : object, $property : property, lastAnswer != $answer )
then
    // Must retract here, in case we're changin a @key property and using equality mode
    retract( $object );
	Object propertyValue = DomainModelSupport.answerToObject( $question.getBasicAnswerType(), $answer, PropertyUtils.getPropertyType( $object, $property ) );
	PropertyUtils.setProperty( $object, $property, propertyValue );

    modify ( $association ) {
        setLastAnswer( $answer );
    }
    insert( $object );
end


rule "Cleanup ununsed associations"
salience 8
when
    $assoc : DomainModelAssociation( $id : questionId, $obj : object )
    (
     not Question( id == $id )
     or
     not Object( this == $obj )
    )
then
    retract( $assoc );
end
