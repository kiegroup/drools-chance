/*
 * Copyright 2011 JBoss Inc
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *       http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

package org.drools.informer

import java.util.ArrayList;
import java.util.Arrays;
import java.util.Collection;
import java.util.Iterator;
import java.util.List;

/*
* A question/group is marked as readonly if it is contained in any group that is readonly.  This means that if you put a question
* in two groups both of which are active, but only one of which is readonly then the question will appear as readonly in both.
*/
rule "propagate readonly from group to subgroups"
no-loop
salience 5
when
	$item : Group( itemStyles : stylesList,
	              stylesList not contains "readonly-inherited"
	           && stylesList not contains "readonly" ) 
	exists (
	    Group( itemList contains $item.id, active == true,
	           stylesList contains "readonly" || stylesList contains "readonly-inherited" ) 
	)
then
    modify ( $item ) {
        addPresentationStyle("readonly-inherited");
    }
end

rule "propagate readonly from group to question"
salience 4
when
	$item : Question( itemStyles : stylesList,
	              stylesList not contains "readonly-inherited"
	           && stylesList not contains "readonly" ) @watch( presentationStyles )
	exists (
	    Group( itemList contains $item.id, active == true,
	           stylesList contains "readonly" || stylesList contains "readonly-inherited" ) 
	)
then
    modify ( $item ) {
        addPresentationStyle("readonly-inherited");
    }
end



rule "remove readonly from group to subgroups"
no-loop
salience 2
when
	$item : Group( itemStyles : stylesList,
	              stylesList contains "readonly-inherited"
	           || stylesList contains "readonly" )
	not (
	    Group( itemList contains $item.id, active == true,
	           stylesList contains "readonly" || stylesList contains "readonly-inherited" ) 
	)
then
    modify ( $item ) {
        removePresentationStyle("readonly-inherited");
    }
end

rule "remove readonly from group to question"
salience 1
when
	$item : Question( itemStyles : stylesList,
	              stylesList contains "readonly-inherited"
	           || stylesList contains "readonly"
	           ) @watch( presentationStyles )
	not (
	    Group( itemList contains $item.id, active == true,
	           stylesList contains "readonly" || stylesList contains "readonly-inherited" ) 
	)
then
    modify ( $item ) {
        removePresentationStyle("readonly-inherited");
    }
end


